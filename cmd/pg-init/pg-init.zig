//! pg-init: PostgreSQL's cluster, made once, and the image's SQL applied.
//!
//! leash runs it before the server, as the postgres user, leashed
//! (forms/postgresql/etc/sv/postgres/service). It does two things, and the
//! server starts only if both succeed:
//!
//!   1. The cluster, if /data/svc/postgres/data has none and never had
//!      one (cluster-made, beside it, says it had: then the data is lost,
//!      and the server stays down rather than start again empty): initdb, with
//!      local connections by peer (a role is its system user's name) and
//!      none by TCP, which the server does not listen on anyway. initdb
//!      makes it in data.new, which becomes data only once whole, so a
//!      start stopped partway leaves no cluster rather than half of one.
//!      initdb runs the server through popen(3) and system(3), which want a
//!      shell; popen-shim.so (cmd/popen-shim/popen-shim.zig), preloaded into
//!      initdb alone, runs its commands without one.
//!   2. The image's SQL: each /usr/share/werewolf-postgres/*.sql, in name order,
//!      in the postgres database, as the superuser, through the server in
//!      single-user mode, which runs while the real server is not yet up.
//!      A form brings its roles, schemas and grants this way, written so
//!      that applying them again changes nothing; the first error stops it.
//!
//! Nothing here comes from outside the image.

const std = @import("std");
const linux = std.os.linux;
const Io = std.Io;
const Dir = Io.Dir;

const svc_dir = "/data/svc/postgres";
const data_dir = svc_dir ++ "/data";
const sql_dir = "/usr/share/werewolf-postgres";
const initdb = "/usr/bin/initdb";
const postgres = "/usr/bin/postgres";
const preload = "/usr/lib/werewolf/popen-shim.so";
/// Left once the cluster is made, beside it: a cluster that is gone while
/// this is not was lost, and is not quietly made again, empty.
const made = "cluster-made";
/// Left on the first start of each boot, in /run, which each boot begins
/// empty: a lock found before it is there is an earlier boot's.
const started = "/run/svc/postgres/pg-init-started";

pub fn main(init: std.process.Init) !void {
    const io = init.io;
    const gpa = init.arena.allocator();
    var svc = try Dir.cwd().openDir(io, svc_dir, .{});
    defer svc.close(io);

    const first_start = blk: {
        if (Dir.cwd().access(io, started, .{})) |_| break :blk false else |_| {}
        Dir.cwd().writeFile(io, .{ .sub_path = started, .data = "" }) catch |err| {
            say(io, "{s}: {s}", .{ started, @errorName(err) });
            break :blk false;
        };
        break :blk true;
    };

    if (svc.access(io, "data/PG_VERSION", .{})) |_| {
        say(io, "keeping the cluster in {s}", .{data_dir});
        // Whole, since data.new becomes data only so: one whose start was
        // cut before cluster-made was kept is marked now.
        if (svc.access(io, made, .{})) |_| {} else |_| try mark(io, svc, made);
        // A power cut leaves the last boot's lock behind, naming a pid this
        // boot may well have given to something else, which the server
        // would take for itself still running. On the first start of a
        // boot, the lock can be no one's: it goes. A later start's lock may
        // be a server of this boot's still stopping, and the server judges
        // that itself.
        if (first_start) {
            if (svc.deleteFile(io, "data/postmaster.pid")) |_| {
                say(io, "removed the lock the last boot left", .{});
            } else |err| switch (err) {
                error.FileNotFound => {},
                else => say(io, "{s}/postmaster.pid: {s}", .{ data_dir, @errorName(err) }),
            }
        }
    } else |_| {
        if (svc.access(io, made, .{})) |_| {
            say(
                io,
                "the cluster in {s} is gone, though one was made here; not making an empty one " ++
                    "over its loss",
                .{data_dir},
            );
            return error.ClusterLost;
        } else |_| {}
        // initdb writes PG_VERSION first and the rest after, so a cluster
        // it was stopped in the middle of would read as made: it makes the
        // cluster beside its place, which takes it only whole.
        if (svc.access(io, "data.new", .{})) |_| {
            say(io, "removing the cluster a stopped start left half made", .{});
            try svc.deleteTree(io, "data.new");
        } else |_| {}
        say(io, "making the cluster in {s}", .{data_dir});
        var env: std.process.Environ.Map = .init(gpa);
        try env.put("PATH", "/usr/bin");
        try env.put("LD_PRELOAD", preload);
        var child = try std.process.spawn(io, .{
            .argv = &.{
                initdb,
                "-D",
                svc_dir ++ "/data.new",
                "-U",
                "postgres",
                "-E",
                "UTF8",
                "--no-locale",
                "--auth-local=peer",
                "--auth-host=reject",
                "--no-instructions",
            },
            .environ_map = &env,
            .stdin = .ignore,
            .stdout = .ignore,
        });
        const term = try child.wait(io);
        if (term != .exited or term.exited != 0) {
            say(io, "{s} failed; see above", .{initdb});
            return error.InitdbFailed;
        }
        svc.rename("data.new", svc, "data", io) catch |err| {
            say(io, "{s} holds something, but no cluster: {s}", .{ data_dir, @errorName(err) });
            return err;
        };
        try syncSvc(io);
        try mark(io, svc, made);
    }

    var names: std.ArrayList([]const u8) = .empty;
    if (Dir.cwd().openDir(io, sql_dir, .{ .iterate = true })) |d| {
        var dir = d;
        defer dir.close(io);
        var it = dir.iterate();
        while (try it.next(io)) |e| {
            if (e.kind == .file and
                std.mem.endsWith(
                    u8,
                    e.name,
                    ".sql",
                )) try names.append(gpa, try gpa.dupe(u8, e.name));
        }
    } else |_| {}
    std.mem.sort([]const u8, names.items, {}, lessThan);
    if (names.items.len == 0) return;

    // -j: a statement ends at a semicolon before an empty line, so a DO
    // block may hold semicolons of its own.
    var sql: std.ArrayList(u8) = .empty;
    for (names.items) |name| {
        const text = try Dir.cwd().readFileAlloc(
            io,
            try gpa.print("{s}/{s}", .{ sql_dir, name }),
            gpa,
            .limited(1 << 20),
        );
        try sql.appendSlice(gpa, text);
        try sql.appendSlice(gpa, "\n\n");
    }
    var child = try std.process.spawn(io, .{
        .argv = &.{
            postgres,
            "--single",
            "-D",
            data_dir,
            "-j",
            "-c",
            "exit_on_error=true",
            "-c",
            "log_checkpoints=false",
            "postgres",
        },
        .stdin = .pipe,
        .stdout = .ignore,
    });
    child.stdin.?.writeStreamingAll(io, sql.items) catch {};
    child.stdin.?.close(io);
    child.stdin = null;
    const term = try child.wait(io);
    if (term != .exited or term.exited != 0) {
        say(io, "the SQL in {s} failed; see above", .{sql_dir});
        return error.SqlFailed;
    }
    say(
        io,
        "applied {d} SQL file{s} from {s}",
        .{ names.items.len, if (names.items.len == 1) "" else "s", sql_dir },
    );
}

/// name, made empty in dir, and on the disk with its entry before this
/// returns.
fn mark(io: Io, dir: Dir, name: []const u8) !void {
    const f = try dir.createFile(io, name, .{});
    defer f.close(io);
    try f.sync(io);
    try syncSvc(io);
}

/// svc_dir's entries on the disk: a rename or a new file in it kept. Its
/// own descriptor, as Dir's may be O_PATH, which cannot be synced.
fn syncSvc(io: Io) !void {
    const rc = linux.open(svc_dir, .{ .ACCMODE = .RDONLY, .DIRECTORY = true, .CLOEXEC = true }, 0);
    const e = if (linux.errno(rc) != .SUCCESS) linux.errno(rc) else blk: {
        defer _ = linux.close(@intCast(rc));
        break :blk linux.errno(linux.fsync(@intCast(rc)));
    };
    if (e == .SUCCESS) return;
    say(io, "syncing {s}: {s}", .{ svc_dir, @tagName(e) });
    return error.SyncFailed;
}

fn lessThan(_: void, a: []const u8, b: []const u8) bool {
    return std.mem.lessThan(u8, a, b);
}

fn say(io: Io, comptime fmt: []const u8, args: anytype) void {
    var buf: [512]u8 = undefined;
    const line = std.mem.print(&buf, "pg-init: " ++ fmt ++ "\n", args) catch return;
    Io.File.stdout().writeStreamingAll(io, line) catch {};
}
